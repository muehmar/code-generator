plugins {
    id 'maven-publish'
    id 'signing'
    id 'code-generator.java'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'code-generator' + project.name.replace('core', '')
            from components.java
            pom {
                name = 'Code Generator'
                description = 'Composable generator to generate source code'
                url = 'https://github.com/muehmar/code-generator'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/muehmar/code-generator/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'muehmar'
                        name = 'Martin MÃ¼hlemann'
                        email = 'poelsi@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/muehmar/code-generator.git'
                    developerConnection = 'scm:git:ssh://github.com/muehmar/code-generator.git'
                    url = 'https://github.com/muehmar/code-generator.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ossrh'
            credentials(PasswordCredentials)
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}